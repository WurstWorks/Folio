plugins {
    id "checkstyle"
    id "eclipse"
    id "idea"
    id "jacoco"
    id "java"
    id "maven-publish"
    id "pmd"
    id "signing"
}

group "org.wurstworks.folio"
version "0.1-SNAPSHOT"
description "WurstWorks Folio"

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly "javax.validation:validation-api:2.0.1.Final"

    implementation "org.slf4j:slf4j-api:1.7.30"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.2"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.2"
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet resources.text.fromFile("config/checkstyle/checkstyle.xsl")
    }
}

tasks.named("test") {
    useJUnitPlatform {
        includeEngines "junit-jupiter"

        maxHeapSize = "2G"
        failFast = true

        reports {
            html.enabled = true
        }

        finalizedBy jacocoTestReport
    }
}

checkstyle {
    toolVersion "8.39"
    configFile file("config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source = "src/main/java"
}

checkstyleTest {
    source = "src/test/java"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}

